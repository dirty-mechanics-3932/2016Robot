plugins {
  id "com.diffplug.gradle.spotless" version "1.3.3"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8

eclipse.classpath.file {
    beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' || entry.kind == 'var' }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// Import Ant build and prefix all task names with 'ant-'.
ant.importBuild('build.xml') { antTaskName ->
    "ant_${antTaskName}".toString()
}

// Set group property for all Ant tasks.
tasks.matching { task ->
    task.name.startsWith('ant_')
}*.group = 'Ant'

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    compile files(ant.properties['wpilib.jar'])
    compile files(ant.properties['networktables.jar'])

    compileOnly "org.projectlombok:lombok:1.16.8"

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.+"
}

spotless {
    java {
        licenseHeaderFile 'spotless/license-header.txt'
        // An import ordering file, exported from Eclipse
        importOrderFile 'spotless/frc.importorder'
        // XML file dumped out by the Eclipse formatter
        eclipseFormatFile 'spotless/frc-format.xml'

        // Eclipse formatter screws up long literals with underscores inside of annotations (see issue #14)
        //    @Max(value = 9_999_999 L) // what Eclipse does
        //    @Max(value = 9_999_999L)  // what I wish Eclipse did
        custom 'Long literal fix', { it.replaceAll('([0-9_]+) [Ll]', '$1L') }

        trimTrailingWhitespace()
    }
    format 'misc', {
        target '**/*.xml', '.gitignore', "*.properties", "*.gradle"
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
    // The default value of PLATFORM_NATIVE is recommended (can be WINDOWS, UNIX, or PLATFORM_NATIVE)
    lineEndings 'PLATFORM_NATIVE'
}

jar {
    manifest {
        attributes("Main-Class": "edu.wpi.first.wpilibj.RobotBase",
                   "Robot-Class": ant.properties['robot.class'],
                   "Class-Path": ".")
    }
}

// Aliases
ant_jar.dependsOn('jar')
ant_clean.dependsOn('clean')
ant_compile.dependsOn('compileJava')
task deploy(dependsOn: ['clean', 'jar', 'ant_deploy'])
task debugDeploy(dependsOn: ['jar', 'ant_debug-deploy'])
task format(dependsOn: 'spotlessApply')
